-- @atlcompiler emftvm
-- @path MM=/TableToMap/MetaModels/Table_MetaModel.ecore
-- @path MM1=/TableToMap/MetaModels/Maps_MetaModel.ecore


module TableToMap;
create OUT: MM1 from IN: MM;

rule Main {
	from
		Table: MM!Table
	to
		Maps: MM1!Maps ( 
			layers <- Sequence{objectLayer}
		),
		objectLayer: MM1!Layer(
			name <- Table.filename,
			points <- Table.rows -> collect(e | thisModule.GetPoints(e))
		)
}
lazy rule GetPoints {
	from
		Row: MM!Row
	to
		Point: MM1!Point (
			lat <- Row.getPointValues('lat'),
			long <- Row.getPointValues('long'),
			value <- Row.getPointValues('value'),
			units <- Row.getPointValues('units'),
			date <- Row.getPointValues('date')
		)
}

helper context MM!Row def: getPointValues(type: String): String =
	let column: Integer = self.findCellPosition(type) in
	if column >= 0 then
		let cell: MM!Cell = self.cells -> get(column) in
		cell.value
	else
		'Value not found'
  endif;
	
helper context MM!Row def: findCellPosition(type: String) : Integer =
	if self.position = 0 then
		let cell : Integer = self.cells -> indexOf(self.cells -> select(c | c.value = type) -> first()) in
		if cell >= 1 then -- no empieza por 0, sino por 1
			cell-1 -- se ajusta la diferencia
		else
	    	-1
		endif
	else
		self.getFirstRow().findCellPosition(type)
	endif;

helper context MM!Row def: getFirstRow() : MM!Row =  -- Este helper devuelve la fila 0 para acceder a la posición de la celda "LAT" en la fila 0
	let table : MM!Table = self.eContainer() in
	table.rows -> first();